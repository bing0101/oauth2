buildscript {
    ext {
        springBootVersion = "1.5.7.RELEASE"
    }

    repositories {
        mavenLocal()
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public"
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

// 打包发行版
apply plugin: "distribution"
distributions {
    main {
        contents.from {
            subprojects.libsDir
        }
    }
}

allprojects {
    group 'com.bing'
    version '1.0-SNAPSHOT'
}

subprojects {
    apply plugin: "java"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenLocal()
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public"
        }
    }

    task createDirs {
        doLast {
            sourceSets*.java.srcDirs*.each { it.mkdirs() }
            sourceSets*.resources.srcDirs*.each { it.mkdirs()}
        }
    }
}

configure(subprojects - project(":oauth2-mapper")) {
    apply plugin: "org.springframework.boot"

    dependencies {
        compile "org.projectlombok:lombok:1.16.10"
        compile "org.springframework.boot:spring-boot-starter-log4j2"
        compile "org.springframework.boot:spring-boot-starter-test"
    }

    configurations {
        all*.exclude module: "spring-boot-starter-logging"
        all*.exclude module: "slf4j-log4j12"
    }
}